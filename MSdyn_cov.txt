model{

mu.b0 ~ dnorm(0, 0.1)
tau.b0 <- 1/(sig.b0 * sig.b0)
sig.b0 ~ dt(0, pow(2.5,-2), 1) T(0,)
mu.b1 ~ dnorm(0, 0.1)
tau.b1 ~ dgamma(0.1, 0.1)
mu.b2 ~ dnorm(0, 0.1)
tau.b2 ~ dgamma(0.1, 0.1)
mu.b3 ~ dnorm(0, 0.1)
tau.b3 ~ dgamma(0.1, 0.1)
mu.b4 ~ dnorm(0, 0.1)
tau.b4 ~ dgamma(0.1, 0.1)
mu.a0 ~ dnorm(0, 0.368)
tau.a0 ~ dgamma(0.1, 0.1)
mu.o0 ~ dnorm(0, 0.368)
tau.o0 <- 1/(sig.o0 * sig.o0)
sig.o0 ~ dt(0, pow(2.5,-2), 1) T(0,)
mu.o1 ~ dnorm(0, 0.368)
tau.o1 <- 1/(sig.o1 * sig.o1)
sig.o1 ~ dt(0, pow(2.5,-2), 1) T(0,)
mu.o2 ~ dnorm(0, 0.368)
tau.o2 <- 1/(sig.o2 * sig.o2)
sig.o2 ~ dt(0, pow(2.5,-2), 1) T(0,)
mu.o3 ~ dnorm(0, 0.368)
tau.o3 <- 1/(sig.o3 * sig.o3)
sig.o3 ~ dt(0, pow(2.5,-2), 1) T(0,)
mu.g0 ~ dnorm(0, 0.1)
tau.g0 <- 1/(sig.g0 * sig.g0)
sig.g0 ~ dt(0, pow(2.5,-2), 1) T(0,)

alpha1 ~ dnorm(0, 0.368)

for(s in 1:nspec){

beta0[s] ~ dnorm(mu.b0, tau.b0)
beta1[s] ~ dnorm(mu.b1, tau.b1)
beta2[s] ~ dnorm(mu.b2, tau.b2)
beta3[s] ~ dnorm(mu.b3, tau.b3)
beta4[s] ~ dnorm(mu.b4, tau.b4)
alpha0[s] ~ dnorm(mu.a0, tau.a0)
omega0[s] ~ dnorm(mu.o0, tau.o0)
omega1[s] ~ dnorm(mu.o1, tau.o1)
omega2[s] ~ dnorm(mu.o2, tau.o2)
omega3[s] ~ dnorm(mu.o3, tau.o3)
gamma0[s] ~ dnorm(mu.g0, tau.g0)

for(j in 1:nsites){

logit(r[nstart[j],j,s]) <- alpha0[s] + alpha1 * days[nstart[j],j]

N[nstart[j],j,s] ~ dpois(lambda[j,s])
log(lambda[j,s]) <- beta0[s] + beta1[s] * density[j] + beta2[s] * edge[nstart[j],j] +
                  beta3[s] * density[j] * edge[nstart[j],j] + beta4[s] * elev[nstart[j],j]

for(t in (nstart[j]+1):nend[j]){

logit(r[t,j,s]) <- alpha0[s] + alpha1 * days[t,j]
logit(omega[t-1,j,s]) <- omega0[s] + omega1[s] * density[j] + omega2[s] * edge[t,j] +
                         omega3[s] * density[j] * edge[t,j]
S[t-1,j,s] ~ dbin(omega[t-1,j,s], N[t-1,j,s])
G[t-1,j,s] ~ dpois(gamma[t-1,s])
N[t,j,s] <- S[t-1,j,s] + G[t-1,j,s]

}#end t
}#end j

for(t in 2:9){
log(gamma[t-1,s]) <- gamma0[s]
}#end t

}#end s

for(k in 1:nobs){
y[k] ~ dbern(p[k])
p[k] <- 1 - pow((1 - r[yr[k], site[k], spec[k]]), N[yr[k], site[k], spec[k]])
}#end k

}